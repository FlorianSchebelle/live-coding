plugins {
    id 'org.openapi.generator' version '6.0.1'
    id 'org.springframework.boot' version '2.7.3' apply false
}

ext {
    basePackageName = 'com.allane.bp.live.coding.api'
    inputLeasingSpec = "$rootDir/contract/leasing.yaml"
}

openApiValidate {
    inputSpec = inputLeasingSpec
}

openApiGenerate {
    generatorName = 'spring'
    configOptions = [
            delegatePattern      : 'true',
            unhandledException   : 'true',
            performBeanValidation: 'true',
            useBeanValidation    : 'true',
            useTags              : 'true',
            dateLibrary          : 'java8'
    ]
    library = 'spring-boot'
    inputSpec = inputLeasingSpec
    supportingFilesConstrainedTo = [
            'ApiUtil.java'
    ]

    globalProperties = [
            apis  : '',
            models: ''
    ]

    additionalProperties = [
            serializableModel: 'true',
            openApiNullable  : 'false'
    ]

    validateSpec = true
    modelNameSuffix = 'DTO'

    generateApiDocumentation = false
    generateModelDocumentation = false
    generateApiTests = false
    generateModelTests = false

    packageName = "${basePackageName}"
    apiPackage = "${basePackageName}.api"
    invokerPackage = "${basePackageName}.invoker"
    modelPackage = "${basePackageName}.model"
}

dependencies {
    implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)

    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.2'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'javax.servlet', name: 'javax.servlet-api'
    implementation group: 'javax.validation', name: 'validation-api'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations'
    implementation group: 'org.hibernate.validator', name: 'hibernate-validator'
    implementation group: 'org.springframework', name: 'spring-context'
    implementation group: 'org.springframework', name: 'spring-web'
}

compileJava.dependsOn tasks.openApiGenerate

sourceSets.main.java.srcDir "${buildDir}/generate-resources/main/src/main/java"